buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'com.jfrog.bintray' version '1.7'
    id 'maven-publish'
    id 'jacoco'
}

group 'com.github.dinsaw'
version '0.7'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.projectlombok', name: 'lombok', version: '1.18.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'navparser'
        version {
            name = project.version
        }
    }
    configurations = ['archives']
    publications = ['MyPublication']
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId rootProject.name
            version project.version
        }
    }
}
